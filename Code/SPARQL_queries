##### Put all SPARQL queries here

### Selects all Parkinson's drugs that interact with at least one of the five dopamine receptors
SELECT ?item ?itemLabel ?filterLabel ?roleLabel ?role
WHERE {
  VALUES ?filter {
  wd:Q21110867
  wd:Q2034004
  wd:Q21110872
  wd:Q308856
  wd:Q21131500}.
  FILTER EXISTS {?item wdt:P129 ?filter.}.
  ?item wdt:P2175 ?role.
  FILTER(?role = wd:Q11085).
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".}
  }

### Query version 2, differentiates agonist and antagonist

SELECT ?item ?itemLabel ?receptorLabel ?roleLabel ?role ?diseaseLabel
WHERE {
  VALUES ?receptor {
  wd:Q21110867
  wd:Q2034004
  wd:Q21110872
  wd:Q308856
  wd:Q21131500}.
  FILTER EXISTS {?item wdt:P129 ?receptor.}.
  VALUES ?role {
  wd:Q600203
  wd:Q3271533}.
  ?item wdt:P2868 ?role.
  ?item wdt:P2175 ?disease.
  FILTER(?disease = wd:Q11085).



  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".}
  }

### Query final version

SELECT ?item ?itemLabel ?receptorLabel ?roleLabel ?diseaseLabel ?structureLabel ?bnsLabel ?formulaLabel
WHERE {
  # Setting ?receptor as dopamine receptors D1-D5
  VALUES ?receptor {
    wd:Q21110867
    wd:Q2034004
    wd:Q21110872
    wd:Q308856
    wd:Q21131500
  }

  # Setting ?role as agonist or antagonist
  VALUES ?role {
    wd:Q389934  # Agonist role
    wd:Q410943  # Antagonist role
  }

  # Filtering drug interactions with receptors by their proper role
  ?item p:P129 ?statement.
  ?statement pq:P2868 ?role.
  ?statement ps:P129 ?receptor.

  # Filter for only Parkinson's treatment
  ?item wdt:P2175 ?disease.
  FILTER(?disease = wd:Q11085)

  # Grab chemical diagram, ball-and-stick model and chemical formula if they exist
  OPTIONAL {?item wdt:P117 ?structure.}
  OPTIONAL {?item wdt:P8224 ?bns.}
  OPTIONAL {?item wdt:P274 ?formula.}

  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en".}
}
